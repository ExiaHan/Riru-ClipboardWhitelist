apply plugin: 'com.android.application'
apply plugin: 'rirutools.magisk'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.github.kr328.clipboard"
        minSdkVersion 29
        targetSdkVersion 29
        versionCode 10002
        versionName "1.0.2"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
}

dependencies {
    api project(":hideapi")
}

magisk {
    output "$buildDir/outputs/riru-clipboard-whitelist.zip"
    
    zip.map(file("src/main/raw/magisk"), "/")
    zip.map(file("$buildDir/outputs/extracted/classes.dex"), "/system/framework/boot-clipboard-whitelist.dex")
    zip.map(file("$buildDir/outputs/extracted/lib/arm64-v8a/") ,"/system/lib64")
    zip.map(file("$buildDir/outputs/extracted/lib/armeabi-v7a/") ,"/system/lib")
}

task extractPackage(type: Copy) {
    from zipTree("$buildDir/outputs/apk/release/module-release-unsigned.apk")
    into "$buildDir/outputs/extracted"
}

afterEvaluate {
    extractPackage.dependsOn assembleRelease
    magiskModule.dependsOn extractPackage
}
